[[phoenix-liveview-basics]]
## Phoenix LiveView Basics

LiveView is an interesting alternative to single-page application (SPA) written
with JavaScript which communicate with the server via JSON and change the
content of the page by themself. Adding a JavaScript Framework to create a SPA 
is adding an other element to your technology stack which has consequences. 
Creating the same functionality with LiveView keeps your technology stack light 
and you don't add one more programming language (JavaScript) to your development 
process.

NOTE: Feel free to skip this chapter if you are not interested in single-page
applications. You can come back any time.

Of course LiveView does use JavaScript to do it's magic but that is done for 
you. You don't have to code any JavaScript yourself. The good to go LiveView 
JavaScript connects via a websocket to your Phoenix application and both 
communicate with each other. On the JavaScript side this is as light weighted 
as possible and uses much less client resources than most JavaScript frameworks.

One need thing about LiveView is that on the initial request Phoenix delivers 
a normal HTML page which includes all the design and content you want it to have. 
So the user doesn't have to wait for JavaScript to do it's work to have a 
usable webpage. As a sideeffect all search engines can use it right away without 
having to rely on JavaScript.

During this chapter I'll show you a couple of examples. They are meant to give 
you an idea of the possiblities. If LiveView is the right choice for your 
project you'll have to invest more time afterwards with experiements and the 
lecture of 

- https://github.com/phoenixframework/phoenix_live_view
- https://hexdocs.pm/phoenix_live_view/Phoenix.LiveView.html

### The Light Switch

The light switch is the "Hello World!" of LiveView. 

NOTE: I stole the light switch idea from the great Pragmatic Studio online
course at https://online.pragmaticstudio.com/courses/liveview

We start with a fresh Phoenix application:

[source,shell]
----
$ mix phx.new demo --live --no-ecto <1>
* creating demo/config/config.exs
* creating demo/config/dev.exs
* creating demo/config/prod.exs

[...]

Fetch and install dependencies? [Yn] <2>

[...]

We are almost there! The following steps are missing:

    $ cd demo

Start your Phoenix app with:

    $ mix phx.server

You can also run your app inside IEx (Interactive Elixir) as:

    $ iex -S mix phx.server
----
<1> '--live' adds all the needed stuff to use LiveView out of the box. For this example we don't need Ecto.
<2> Always `Y` and depending on your internet connection a good time for a cup of coffee.

The aim of this demo is a new webpage with the path `/light` which offers a
status of a virtual light bulb and a switch functionality to turn on/off that
light bulb. All this without reloading the page and without any JavaScript.

First we have to add the route:

lib/hello_world_web/router.ex
[source,elixir]
----
defmodule DemoWeb.Router do
  use DemoWeb, :router

  [...]

  scope "/", DemoWeb do
    pipe_through :browser

    live "/", PageLive, :index <1>
    live "/light", LightLive <2>
  end

  [...]
----
<1> Because we created the app with the `--live` switch the default root path is already a live view. Therefor the `live` macro is used here (at the beginning of the line) instead of the traditional `get`.
<2> This is the new `light` route which leads to the `LightLive` module.

LiveView modules are located in the `lib/demo_web/live/` directory. There we have to create 
our new `LightLive` module:

lib/demo_web/live/light_live.ex
[source,elixir]
----
defmodule DemoWeb.LightLive do
  use DemoWeb, :live_view

  def render(assigns) do <1>
    ~L"""
    <h1>The light is off.</h1>
    """
  end
end
<1> The `render/1` function renders the template. We use the `~L` sigil to define the template.
----

[IMPORTANT] 
====
We use the `~L` sigil to store the template code in this controller. It is
easier for the purpose of this document. For bigger templates it makes more
sense to store them in their own file. That would be a LiveEEx Templates
(`.leex` extension) and would be named `lib/demo_web/live/light_live.html.leex`
in this case and include `<h1>The light is off.</h1>`. When you have that file
you don't need the `render/1` function at all.

Have a look at https://hexdocs.pm/phoenix_live_view/Phoenix.LiveView.html for more information.
====
indexterm:["LiveEEx Templates"]

Now we can open the browser and open the URL `http://localhost:4000/light`

image::liveview-static.png[http://localhost:4000/light]

Our aim is to change the world `off` to `on` and trigger that by a button. For that to 
happen we first have to replace the current word with a variable and we have to assign 
that variable to the socket struct (which is used to transport that information). We assign 
the initial state of the variable `light_bulb_status` in the `mount/3` function.

lib/demo_web/live/light_live.ex
[source,elixir]
----
defmodule DemoWeb.LightLive do
  use DemoWeb, :live_view

  def mount(_params, _session, socket) do <1>
    socket = assign(socket, :light_bulb_status, "off") <2>
    {:ok, socket}
  end

  def render(assigns) do
    ~L"""
    <h1>The light is <%= @light_bulb_status %>.</h1>
    """
  end
end
----
<1> Out of all the posssible parameters of `mount/3` we only need the `socket` struct for our example.
<2> We assign the value `off` to the variable `light_bulb_status`

The browser automatically reloads but the page's content hasn't changed. Only we know that the `off` 
is not a static content any more.

To turn on the light bulb we need a button:

[source,elixir]
----
def render(assigns) do
  ~L"""
  <h1>The light is <%= @light_bulb_status %>.</h1>
  <button phx-click="on">On</button> <1>
  """
end
----
<1> The button tag includes `phx-click="on"` which is special Phoenix code to trigger an event.

Now we see the button on the webpage:

image::liveview-on-button.png[http://localhost:4000/light]

But clicking on the button doesn't do anything. We have to add a `handle_event/3` function for the `on` event:

lib/demo_web/live/light_live.ex
[source,elixir]
----
defmodule DemoWeb.LightLive do
  use DemoWeb, :live_view

  def mount(_params, _session, socket) do
    socket = assign(socket, :light_bulb_status, "off")
    {:ok, socket}
  end

  def render(assigns) do
    ~L"""
    <h1>The light is <%= @light_bulb_status %>.</h1>
    <button phx-click="on">On</button>
    """
  end

  def handle_event("on", _value, socket) do <1>
    socket =
      socket
      |> assign(:light_bulb_status, "on") <2>

    {:noreply, socket}
  end
end
----
<1> We don't need the `_value` parameter. Just the first parameter to match the function and the socket struct.
<2> We set the `light_bulb_status` variable to `on`.

[NOTE] 
====
To use the pipe operator in the `handle_event/3` function is kind of overkill for 
just one variable. In that case it would make sense to use this code:

```
def handle_event("on", _value, socket) do
  {:noreply, assign(socket, :light_bulb_status, "on")}
end
```

Same argument works for the `mount/3` function:

```
def mount(_params, _session, socket) do
  {:ok, assign(socket, :light_bulb_status, "off")}
end
```
====

No we can load the page having the light `off`. After clicking on the button 
the text updates to `on`.

image::liveview-on-button-after-clicking.png[http://localhost:4000/light]

But it would be nice to add a second button so that we can switch the light off 
again. In addition we have to add an other event handler for the `off` event:

lib/demo_web/live/light_live.ex
[source,elixir]
----
defmodule DemoWeb.LightLive do
  use DemoWeb, :live_view

  def mount(_params, _session, socket) do
    socket = assign(socket, :light_bulb_status, "off")
    {:ok, socket}
  end

  def render(assigns) do
    ~L"""
    <h1>The light is <%= @light_bulb_status %>.</h1>
    <button phx-click="on">On</button>
    <button phx-click="off">Off</button>
    """
  end

  def handle_event("on", _value, socket) do
    socket =
      socket
      |> assign(:light_bulb_status, "on")

    {:noreply, socket}
  end

  def handle_event("off", _value, socket) do
    socket =
      socket
      |> assign(:light_bulb_status, "off")

    {:noreply, socket}
  end  
end
----

Now we have a webpage with two buttons which work to turn the imaginary light on
and off. But I don't like that both buttons are active all the time. That is bad
UX. Let's fix that:

lib/demo_web/live/light_live.ex
[source,elixir]
----
defmodule DemoWeb.LightLive do
  use DemoWeb, :live_view

  def mount(_params, _session, socket) do
    socket =
      socket
      |> assign(:light_bulb_status, "off")
      |> assign(:on_button_status, "") <1>
      |> assign(:off_button_status, "disabled")

    {:ok, socket}
  end

  def render(assigns) do
    ~L"""
    <h1>The light is <%= @light_bulb_status %>.</h1>
    <button phx-click="on" <%= @on_button_status %>>On</button>
    <button phx-click="off" <%= @off_button_status %>>Off</button> <2>
    """
  end

  def handle_event("on", _value, socket) do
    socket =
      socket
      |> assign(:light_bulb_status, "on")
      |> assign(:on_button_status, "disabled") <3>
      |> assign(:off_button_status, "")

    {:noreply, socket}
  end

  def handle_event("off", _value, socket) do
    socket =
      socket
      |> assign(:light_bulb_status, "off")
      |> assign(:on_button_status, "")
      |> assign(:off_button_status, "disabled")

    {:noreply, socket}
  end
end
----
<1> We assign a value for the `on_button_status` and `off_button_status` to make the on button active and the off button inactive at the start.
<2> We use the `@off_button_status` to disable the off button right at the beginning.
<3> We toggle the values of the buttons.

We are all set. The buttons work in the way a user would like them to work. All
without writing a single line of JavaScript. Phoenix LiveView takes care of all
that. We can concentrate on the application development with Elixir.

Please open your browser at http://localhost:4000/light and give it a try.

image::liveview-working-on-off-button.png[http://localhost:4000/light]

### The Clock

The clock is an example of content that is pushed and triggered by the server. 
No interaction by the user. It displays the current server time on a webpage.

We start with a fresh Phoenix application:

[source,shell]
----
$ mix phx.new clock --live --no-ecto <1>
* creating demo/config/config.exs
* creating demo/config/dev.exs

[...]

$ cd clock
----
<1> No need to complicate things by adding Ecto to this example.

The first thing is always to add a new route for the LiveView:

lib/clock_web/router.ex
[source,elixir]
----
defmodule ClockWeb.Router do
  use ClockWeb, :router

  [...]

  scope "/", ClockWeb do
    pipe_through :browser

    live "/", PageLive, :index
    live "/clock", ClockLive <1>
  end

  [...]
----
<1> Our new clock will be available at http://localhost:4000/clock

lib/clock_web/live/clock_live.ex
[source,elixir]
----
defmodule ClockWeb.ClockLive do
  use ClockWeb, :live_view

  def mount(_params, _session, socket) do
    if connected?(socket) do <1>
      :timer.send_interval(1000, self(), :tick) <2>
    end

    socket = assign_current_time(socket) <3>
    {:ok, socket}
  end

  def render(assigns) do
    ~L"""
    <h1><%= @now %></h1>
    """
  end

  def handle_info(:tick, socket) do <4>
    socket = assign_current_time(socket)

    {:noreply, socket}
  end

  def assign_current_time(socket) do
    now =
      Time.utc_now() <5>
      |> Time.to_string()
      |> String.split(".") <6>
      |> hd

    assign(socket, now: now) <7>
  end
end
----
<1> `mount/3` gets called twice. The first time when the inital HTTP-Request gets answered. That would be the initial webpage. And a second time when the LiveView JavaScript client has connected to the websocket. We want to start our timer at that second request.
<2> This is a bit of Erlang code which fires up a timer which calls the `tick/1` method every 1,000 milliseconds.
<3> The `assign_current_time/1` function gets called to add the `now` value to the `socket` struct.
<4> `handle_info/2` gets called by the 1 second timer to update the value of `now`.
<5> `Time.utc_now()` returns the current time on the server.
<6> This pipeline is just used so that the time is displayed without the milliseconds.
<7> Returns a `socket` struct.

Fire up the webserver with `mix phx.server` and open http://localhost:4000/clock in your browser.

image::liveview-clock.png[http://localhost:4000/clock]

### A Counter

This LiveView example will generate a counter website. It starts with 0 and each
time you click on a button it will increase by one.

[source,shell]
----
$ mix phx.new demo --live --no-ecto
[...]
$ cd demo
----

lib/demo_web/router.ex
[source,elixir]
----
scope "/", DemoWeb do
  pipe_through :browser

  live "/", PageLive, :index
  live "/counter", CounterLive <1>
end
----
<1> The counter will be available at http://localhost:4000/counter

Now we have to create the `lib/demo_web/live/counter_live.ex` file and fill it
with live:

lib/demo_web/live/counter_live.ex
[source,elixir]
----
defmodule DemoWeb.CounterLive do
  use DemoWeb, :live_view

  def mount(_params, _session, socket) do
    socket = assign(socket, :counter, 0) <1>
    {:ok, socket}
  end

  def render(assigns) do
    ~L"""
    <h1>Current count: <%= @counter %></h1> <2>
    <button phx-click="inc">+1</button> <3>
    <button phx-click="reset">Reset</button> <4>
    """
  end

  def handle_event("inc", _, socket) do
    socket = update(socket, :counter, &(&1 + 1)) <5>
    {:noreply, socket}
  end

  def handle_event("reset", _, socket) do
    socket = assign(socket, :counter, 0) <6>
    {:noreply, socket}
  end
end
----
<1> We assign the value of 0 to `counter`.
<2> Display the value of `@counter`.
<3> Increase by 1 button.
<4> Reset the counter to 0 button.
<5> `update/3` is used to call a capture function to increase the value of the `counter` by 1.
<6> We reset the `counter` to 0 here.

Please open your browser at http://localhost:4000/counter and give it a try.

image::liveview_counter.png[http://localhost:4000/counter]

#### assign vs update
indexterm:["assign (LiveView)", "update (LiveView)"]

In the counter example we use this code with an `update/3` function:

[source,elixir]
----
def handle_event("inc", _, socket) do
  socket = update(socket, :counter, &(&1 + 1))
  {:noreply, socket}
end
----

We could achieve the same result with this code which uses the `assign/3`
function. But for that we'd have to get the value of `counter` via the 
`socket` struct:

[source,elixir]
----
def handle_event("inc", _, socket) do
  counter = socket.assigns.counter + 1
  socket = assign(socket, :counter, counter)
  {:noreply, socket}
end
----

Both versions work fine. Of course the `update/3` is a bit more elegant. But you
will get no medal for using it. Use the function you feel more comfortable with.

### Airport Code Search

In this LiveView example we create a search field for airport codes.

[source,shell]
----
$ mix phx.new travelagent --live --no-ecto
$ cd travelagent
----

We begin with the route of the new page:

lib/travelagent_web/router.ex
[source,elixir]
----
[...]
scope "/", TravelagentWeb do
  pipe_through :browser

  live "/", PageLive, :index
  live "/search", SearchLive
end
[...]
----

Next we need to create a module which holds that list of airports and a search 
function. We'll put this into `lib/travelagent/airports.ex`

lib/travelagent/airports.ex
[source,elixir]
----
defmodule Travelagent.Airports do
  def search_by_code(""), do: [] <1>

  def search_by_code(code) do <2>
    list_airports()
    |> Enum.filter(&String.starts_with?(&1.code, code))
  end

  def list_airports do <3>
    [
      %{name: "Berlin Brandenburg", code: "BER"},
      %{name: "Berlin Schönefeld", code: "SXF"},
      %{name: "Berlin Tegel", code: "TXL"},
      %{name: "Bremen", code: "BRE"},
      %{name: "Köln/Bonn", code: "CGN"},
      %{name: "Dortmund", code: "DTM"},
      %{name: "Dresden", code: "DRS"},
      %{name: "Düsseldorf", code: "DUS"},
      %{name: "Frankfurt", code: "FRA"},
      %{name: "Frankfurt-Hahn", code: "HHN"},
      %{name: "Hamburg", code: "HAM"},
      %{name: "Hannover", code: "HAJ"},
      %{name: "Leipzig Halle", code: "LEJ"},
      %{name: "München", code: "MUC"},
      %{name: "Münster Osnabrück", code: "FMO"},
      %{name: "Nürnberg", code: "NUE"},
      %{name: "Paderborn Lippstadt", code: "PAD"},
      %{name: "Stuttgart", code: "STR"}
    ]
  end
end
----
<1> A search for an empty string results in an empty list.
<2> `search_by_code/1` searches for airport code's fragments and full codes.
<3> We hardcode a list of German airports here. In a real application this would be longer and probably database driven.

This time we don't use the `~L` sigil in the controller but a LiveEEx Template
file:

lib/travelagent_web/live/search_live.html.leex
[source,html]
----
<form phx-submit="airport_code_search">
  <fieldset>
    <label for="nameField">Airport Code</label>
    <input type="text" name="airport_code" value="<%= @airport_code %>"
    placeholder="e.g. FRA" 
    autofocus autocomplete="off" /> <1>
    <input class="button-primary" type="submit" value="Search Airport">
  </fieldset>
</form>

<%= unless @airports == [] do %> <2>
  <h2>Search Results</h2>
  <table>
    <thead>
      <tr>
        <th>Airport Code</th>
        <th>Name</th>
      </tr>
    </thead>
    <tbody>
      <%= for airport <- @airports do %>
      <tr>
        <td><%= airport.code %></td>
        <td><%= airport.name %></td>
      </tr>
      <% end %>
    </tbody>
  </table>
<% end %>
----
<1> I think it is always a curtesy to the user to set the first input field to `autofocus`. And we add an `autocomplete="off"` just to be sure that the browser doesn't interact with us.
<2> When search results in a list of airport a table with the results will be displayed.

Lastly the TravelagentWeb.SearchLive module:

lib/travelagent_web/live/search_live.ex
[source,elixir]
----
defmodule TravelagentWeb.SearchLive do
  use TravelagentWeb, :live_view
  alias Travelagent.Airports <1>

  def mount(_params, _session, socket) do
    socket =
      socket
      |> assign(:airport_code, "") <2>
      |> assign(:airports, [])

    {:ok, socket}
  end

  def handle_event(
        "airport_code_search",
        %{"airport_code" => airport_code},
        socket
      ) do
    airport_code = String.upcase(airport_code) <3>

    socket =
      socket
      |> assign(:airport_code, airport_code) <4>
      |> assign(:airports, Airports.search_by_code(airport_code)) <5>

    {:noreply, socket}
  end
end
----
<1> One can argue if this `alias` is needed here. It results in a shorter line of code later on.
<2> We assign the `airport_code` to empty and assign an empty list to `airports`.
<3> We auto upcase the search string.
<4> The upcased search string gets returned to the view.
<5> The result of the search gets returned to the view.

Please open your browser at http://localhost:4000/search and give it a try.

image::liveview_airport_search.png[http://localhost:4000/search]

#### Airport Code Search Autocomplete

It would be nice to have some sort of autocomplete functionality for the airport
code search. So that when I start to enter an `h` I'll get all airports which
codes start with an `h`. Without having to click on the `Search Airport` button. 
Lucky us we only have to do a couple of changes in the LiveEEx Template file.

lib/travelagent_web/live/search_live.html.leex
[source,html]
----
<form phx-change="airport_code_search"> <1>
  <fieldset>
    <label for="nameField">Airport Code</label>
    <input type="text" name="airport_code" value="<%= @airport_code %>"
    placeholder="e.g. FRA" 
    autofocus autocomplete="off" />
  </fieldset>
</form>

<%= unless @airports == [] do %>
  <h2>Search Results</h2>
  <table>
    <thead>
      <tr>
        <th>Airport Code</th>
        <th>Name</th>
      </tr>
    </thead>
    <tbody>
      <%= for airport <- @airports do %>
      <tr>
        <td><%= airport.code %></td>
        <td><%= airport.name %></td>
      </tr>
      <% end %>
    </tbody>
  </table>
<% end %>
----
<1> We just have to use `phx-change` for the form. This means that each change triggers `handle_event/3`.

Please open your browser at http://localhost:4000/search and give it a try.

image::liveview_airport_search_autocomplete.png[http://localhost:4000/search]

